apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

repositories {
    jcenter()
    google()
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        google()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.2'
    }
}

ext {
    jacocoVersion = "0.7.4.201502262128"
}

dependencies {
    implementation project(':')

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    // dependencies for instrumented tests
    // JUnit4 Rules
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    // Android JUnit Runner
    androidTestImplementation 'com.android.support.test:runner:1.0.1'

    jacocoAnt "org.jacoco:org.jacoco.ant:${project.jacocoVersion}"
    jacocoAgent "org.jacoco:org.jacoco.agent:${project.jacocoVersion}"
    androidJacocoAgent "org.jacoco:org.jacoco.agent:${project.jacocoVersion}"
    androidJacocoAnt "org.jacoco:org.jacoco.ant:${project.jacocoVersion}"
    androidJacocoAnt "org.jacoco:org.jacoco.core:${project.jacocoVersion}"
    androidJacocoAnt "org.jacoco:org.jacoco.report:${project.jacocoVersion}"
    androidJacocoAnt "org.jacoco:org.jacoco.agent:${project.jacocoVersion}"
    androidTestCompile 'junit:junit:4.12'
    implementation 'commons-io:commons-io:2.6'
}

android {
    compileSdkVersion 26

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

task combinedTestReport(type: JacocoReport) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}
