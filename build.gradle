import com.github.spotbugs.snom.SpotBugsTask

buildscript {
    ext.kotlin_version = '1.4.21'
    ext.junit_version = '4.13.1'
    repositories {
        google()
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath('com.hiya:jacoco-android:0.2')
        classpath 'gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.15.0"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.hiya.jacoco-android'
apply plugin: "com.github.spotbugs"
apply plugin: 'io.gitlab.arturbosch.detekt'

repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }

    flatDir {
        dirs 'libs'
    }
}

configurations {
    ktlint

    all {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'org.ogce', module: 'xpp3' // xpp3 is for plain java, Android uses kxml2

        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

ext {
    lombokVersion = "1.18.16"
}

dependencies {
    implementation 'org.apache.jackrabbit:jackrabbit-webdav:2.13.1'
    api 'com.squareup.okhttp3:okhttp:3.12.6' // >= 3.13 requires Android 5 (API 21)
    implementation 'com.gitlab.bitfireAT:dav4jvm:1.0.1' // in transition phase, we use old and new libs
    implementation 'org.parceler:parceler-api:1.1.13'
    annotationProcessor 'org.parceler:parceler:1.1.13'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation 'androidx.annotation:annotation:1.1.0'
    compileOnly 'com.google.code.findbugs:annotations:3.0.1u2'

    //noinspection AnnotationProcessorOnCompilePath
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    ktlint "com.pinterest:ktlint:0.40.0"

    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
    spotbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.4.7'

    // dependencies for tests
    testImplementation "junit:junit:$junit_version"
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'

    // dependencies for instrumented tests
    // JUnit4 Rules
    androidTestImplementation "junit:junit:$junit_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    // Android JUnit Runner
    androidTestImplementation 'androidx.test:runner:1.3.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'commons-io:commons-io:2.8.0'
    androidTestImplementation 'net.bytebuddy:byte-buddy:1.10.18'
}

spotbugs {
    toolVersion = '3.1.12'
}

android {
    compileSdkVersion 29

    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    lintOptions {
        abortOnError false
        htmlReport true
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "TEST_SERVER_URL", "${NC_TEST_SERVER_BASEURL}"
        testInstrumentationRunnerArgument "TEST_SERVER_USERNAME", "${NC_TEST_SERVER_USERNAME}"
        testInstrumentationRunnerArgument "TEST_SERVER_PASSWORD", "${NC_TEST_SERVER_PASSWORD}"
        testInstrumentationRunnerArguments disableAnalytics: 'true'

        multiDexEnabled true
    }

    tasks.register("spotbugsReport", SpotBugsTask) {
        ignoreFailures = true // should continue checking
        effort = "max"
        reportLevel = "medium"
        classes = fileTree("$project.buildDir/intermediates/javac/debug/classes/")
        excludeFilter = file("${project.rootDir}/scripts/analysis/spotbugs-filter.xml")

        reports {
            xml.enabled = false
            html {
                enabled = true
                destination = file("$project.buildDir/reports/spotbugs/spotbugs.html")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

tasks.register("combinedTestReport", JacocoReport) {

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }

    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/debugAndroidTest/connected/*coverage.ec'
    ])
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "--reporter=plain", "--reporter=plain,output=${buildDir}/ktlint.txt,src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

detekt {
    reports {
        xml {
            enabled = false
        }
    }
    config = files("detekt.yml")
    input = files("src/")
}
